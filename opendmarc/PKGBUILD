# Maintainer: Arthur Titeica <arthur.titeica@gmail.com>

pkgname=opendmarc
pkgver=1.1.3
pkgrel=2
pkgdesc="Free open source software implementation of the DMARC specification"
arch=('i686' 'x86_64')
url="http://www.trusteddomain.org/opendmarc/"
license=('custom')
depends=('libidn' 'perl')
makedepends=('libmilter')
optdepends=('python: run opendmarc scripts'
            'perl-switch: run opendmarc scripts'
            'perl-dbi: run opendmarc scripts'
            'perl-dbd-mysql: run opendmarc scripts'
            'perl-http-message: run opendmarc scripts')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        opendmarc.service
        opendmarc.tmpfiles)

sha256sums=('4f9511ebd8cefb6e5a4d836f6285a1b6e4d682daec79fdba9fc7925b5f4fcfca'
            'SKIP'
            '9e8d3e88da880f3f081c8c72428da910701b4e993e750644bfb0165758f291f3'
            'c96fe9b2e4d15c592f2fd5853288bcc9872e83e80dbdcf60e709fac306bbaecd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --sysconfdir=/etc/$pkgname

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # sample config
  install -D -m644 "$srcdir"/$pkgname-$pkgver/$pkgname/opendmarc.conf.sample "$pkgdir"/etc/$pkgname/opendmarc.conf.sample
  # License
  install -D -m644 "$srcdir"/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/usr/share/doc/$pkgname/LICENSE
  # systemd service
  install -D -m644 "$srcdir/$pkgname.service" "${pkgdir}/usr/lib/systemd/system/$pkgname.service"
  # tmpfile
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

}

# vim:set ts=2 sw=2 et:
